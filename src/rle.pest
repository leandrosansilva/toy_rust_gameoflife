// Documented at http://www.conwaylife.com/wiki/Run_Length_Encoded

File = _{ SOI ~ Life ~ EOI } 

Life = { Metas ~ Header ~ Body? }

Metas = { Meta* }
Meta = @{ "#" ~ MetaType ~ MetaContent ~ NEWLINE }
MetaType = @{ "C" | "c" | "N" | "O" | "P" | "R" | "r" }
MetaContent = @{ PRINTABLE }

Header = { "x" ~ "=" ~ X ~ "," ~ "y" ~ "=" ~ Y }
X = ${ NUMBER }
Y = ${ NUMBER }

Body = { Patterns ~ "!" ~ Garbage? }
Patterns = { Pattern+ }
Pattern = ${ DeadOrAlive | EndOfLinePattern }
DeadOrAlive = ${ (RunCount? ~ Tag) }
RunCount = ${ NUMBER }
Tag = ${ DeadTag | AliveTag }
DeadTag = ${ "b" }
AliveTag = ${ "o" }
EndOfLinePattern = ${ "$" }
Garbage = _{ ANY+ }

NUMBER = @{ ASCII_DIGIT+ }
PRINTABLE = @{ (!NEWLINE ~ ANY)* }
WHITESPACE = _{ (" " | NEWLINE)+ }
